// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
    // xphaseone is the output of whether x is zerod or stays the same
    // xphasetwo is the output of whether xphaseone is negated or not negated
    Mux16(a=x, b[0..15]=false, sel=zx, out=xphaseone);
    Not16(in=xphaseone, out=notxphaseone);
    Mux16(a=xphaseone, b=notxphaseone, sel=nx, out=xphasetwo);

    // yphaseone is the output of whether y is zerod or stays the same
    // yphasetwo is the output of whether yphaseone is negated or not negated
    Mux16(a=y, b[0..15]=false, sel=zy, out=yphaseone);
    Not16(in=yphaseone, out=notyphaseone);
    Mux16(a=yphaseone, b=notyphaseone, sel=ny, out=yphasetwo);

    // compute out = x + y (if 1) or x & y (if 0)
    // xyphase3 is the output of whether x is added or anded to y
    Add16(a=xphasetwo, b=yphasetwo, out=xaddy);
    And16(a=xphasetwo, b=yphasetwo, out=xandy);
    Mux16(a=xandy, b=xaddy, sel=f, out=xyphase3);

    // negate the out output
    Not16(in=xyphase3, out=notxyphase3);
    // Note: According to HDL, I can't connect the gate's output pin to a Part
    Mux16(a=xyphase3, b=notxyphase3, sel=no, out=out, out=preout, out[15]=outmsb);

    // 1 if (out == 0), 0 otherwise
    // Needed to create custom chip, because according to HDL, I can't index an internal pin
    IsZero(in=preout, out=zr);

    // 1 if (out < 0),  0 otherwise
    And(a=true, b=outmsb, out=ng);
}